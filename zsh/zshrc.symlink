#
# Executes commands at the start of an interactive session.
#
# Authors:
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#

ZSH=$HOME/.dotfiles
DEFAULT_USER="jrubin"

autoload promptinit
fpath=($ZSH/zsh/prezto-themes $fpath)
promptinit

# Source Prezto.
if [[ -s "${ZDOTDIR:-$HOME}/.zprezto/init.zsh" ]]; then
  source "${ZDOTDIR:-$HOME}/.zprezto/init.zsh"
fi

# Customize to your needs...

typeset -U config_files
config_files=($ZSH/**/*.zsh~$ZSH/zsh/prezto/*~$ZSH/vim/bundle/*~$ZSH/vim/backups/*)

# load the path files
for file in ${(M)config_files:#*/path.zsh}; do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}; do
  source $file
done

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}; do
  source $file
done

unset config_files
